<HTML><HEAD><TITLE>{[incr Tk]} - Widget manual page</TITLE></HEAD><BODY>
<H3><A NAME="M2">NAME</A></H3>
Widget - base class for mega-widgets within a frame
<H3><A NAME="M3">INHERITANCE</A></H3>
itk::Archetype &lt;- itk::Widget
<H3><A NAME="M4">STANDARD OPTIONS</A></H3>
<B>background</B>	<B>cursor</B>
<P>
See the &quot;options&quot; manual entry for details on the standard options.
<H3><A NAME="M5">DESCRIPTION</A></H3>
The <B>Widget</B> class inherits everything from the <B>Archetype</B>
class, and adds a Tk frame called the &quot;hull&quot; component to represent
the body of the mega-widget.  The window class name for the hull
is set to the most-specific class name for the mega-widget.
The protected variable <B>itk_interior</B> contains the window
path name for the &quot;hull&quot; component.  Derived classes specialize
this widget by packing other widget components into the hull.
<P>
Since the hull for the <B>Widget</B> class is implemented with a
Tk frame, mega-widgets in the <B>Widget</B> class can be packed
into other frames and toplevels.

<H3><A NAME="M6">COMPONENTS</A></H3>
Name:	<B>hull</B><BR>
Class:	<B><A HREF="../TkCmd/frame.htm">Frame</A></B><BR>
<DL><DD>
The &quot;hull&quot; component acts as the body for the entire mega-widget.
Other components are packed into the hull to further specialize
the widget.

</DL>
<H3><A NAME="M7">EXAMPLE</A></H3>
The following example implements a simple <B>TextDisplay</B>
mega-widget.  It creates a read-only display of text with
a text widget and a scrollbar.
<PRE>option add *TextDisplay.wrap none widgetDefault
option add *TextDisplay.textBackground ivory widgetDefault
option add *TextDisplay.width 40 widgetDefault
option add *TextDisplay.height 10 widgetDefault

class TextDisplay {
    inherit itk::Widget

    constructor {args} {
        itk_component add text {
            text $itk_interior.info -state disabled 
                -yscrollcommand [code $itk_interior.sbar set]
        } {
            usual
            keep -tabs -wrap -width -height
            rename -background -textbackground textBackground Background
        }
        pack $itk_component(text) -side left -expand yes -fill both

        itk_component add scrollbar {
            scrollbar $itk_interior.sbar 
                -command [code $itk_interior.info yview]
        }
        pack $itk_component(scrollbar) -side right -fill y

        eval itk_initialize $args
    }

    public method display {info}
    public method append {info}
}

body TextDisplay::display {info} {
    $itk_component(text) configure -state normal
    $itk_component(text) delete 1.0 end
    $itk_component(text) insert 1.0 $info
    $itk_component(text) configure -state disabled
}

body TextDisplay::append {info} {
    $itk_component(text) configure -state normal
    $itk_component(text) insert end $info
    $itk_component(text) configure -state disabled
}

usual TextDisplay {
    keep -background -cursor -foreground -font
    keep -activebackground -activerelief
    keep -highlightcolor -highlightthickness
    keep -insertbackground -insertborderwidth -insertwidth
    keep -insertontime -insertofftime
    keep -selectbackground -selectborderwidth -selectforeground
    keep -textbackground -troughcolor
}

#
# EXAMPLE:  Display the /etc/passwd file
#
TextDisplay .file -background red
pack .file

	.file display [exec cat /etc/passwd]</PRE>
<H3><A NAME="M8">KEYWORDS</A></H3>
<A href="../Keywords/I.htm#itk">itk</A>, <A href="../Keywords/A.htm#Archetype">Archetype</A>, <A href="../Keywords/W.htm#Widget">Widget</A>, <A href="../Keywords/M.htm#mega-widget">mega-widget</A>
<HR><PRE>
<A HREF="../copyright.htm">Copyright</A> &#169; 1993-1998 Lucent Technologies, Inc.
<A HREF="../copyright.htm">Copyright</A> &#169; 1995-1997 Roger E. Critchlow Jr.</PRE>
</BODY></HTML>
